#version 430

#define GRID_UNDEF 4294967295

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;



// Struct for atom
struct AtomStruct
{
    vec3 center;
    float radius;
    vec4 proteinID;
};



// SSBOs
layout(std430, binding = 0) buffer AtomsBuffer           { AtomStruct atom[];};
layout(std430, binding = 1) buffer PositionsBuffer       { vec4 pos[];       };
layout(std430, binding = 2) buffer GridcellsBuffer       { uint gcell[];     };
layout(std430, binding = 3) buffer GridindicesBuffer     { uint gndx[];      };
layout(std430, binding = 4) buffer GridcountBuffer       { int  gridcnt[];   };
layout(std430, binding = 5) buffer GridoffsetBuffer      { int  gridoff[];   };
layout(std430, binding = 8) buffer GridBuffer            { uint grid[];      };
layout(std430, binding =12) buffer UnsortedIndexBuffer   { uint undx[];      };
layout(std430, binding =13) buffer SearchResultBuffer    { int  searchRes[]; };



uniform int     pnum;
uniform float   radius2; // radius^2
uniform ivec3   gridRes;
uniform int     gridAdjCnt;
uniform int     searchCellOff;
uniform int     gridAdj[216];



void checkIfAtomsAreInRadius(uint cell, vec4 position, int pID, inout int isInRadius)
{
    if (gridcnt[cell] != 0) {
        uint cfirst = gridoff[cell];
        uint clast  = cfirst + gridcnt[cell];

        // iterate over all atoms
        for (uint cndx = cfirst; cndx < clast; cndx++) {
            uint j = grid[cndx];

            // is atom j within search radius?
            vec4 distance = position - pos[j];
            float d2 = (distance.x * distance.x) + (distance.y * distance.y) + (distance.z * distance.z);
            if (d2 < radius2) {

                // do both atoms belong to different proteins?
                int pID2 = int(atom[undx[j]].proteinID.x);
                if (pID != pID2) {
                    isInRadius = 1;
                }
            }
        }
    }
}

void main() {
    // get element index
    uint i = gl_GlobalInvocationID.x;
    if (i >= pnum) return;

    // get cell for the corresponding atom
    uint icell = gcell[i];
    if (icell == GRID_UNDEF) return; // particle is outside the grid

    // important informations for the compare
    vec4 position = pos[i];
    uint uidx     = undx[i];
    int  pID      = int(atom[uidx].proteinID.x);

    /*
     * check for all adjacent grid cells within the search radius of this atom
     */
    int isInRadius = 0;
    uint startCell = icell - searchCellOff;
    for (int cellIdx = 0; cellIdx < gridAdjCnt; cellIdx++) {
        uint currentCell = startCell + gridAdj[cellIdx];
        checkIfAtomsAreInRadius(currentCell, position, pID, isInRadius);
    }

    // assign isInRadius to this field
    searchRes[uidx] = isInRadius;

}